
require:
   quaint-google-fonts
   quaint-nav
   path
   fs
   node-sass
   deepmerge
   postcss
   autoprefixer

provide:
   defaults


read-sheets{@, options} =
   if options._sheets:
      options
   else:
      read-file = options.read-file or [[@] and [x -> @read-resource-text{.resource, x}]]
      style-root = path.join{__dirname, "../sass"}
      if read-file:
         options & {
            _sheets = {
               common = read-file{path.join{style-root, "_common.sass"}}
               main = read-file{path.join{style-root, "main.sass"}}
               nav = read-file{path.join{style-root, "nav.sass"}}
               extra =
                  if options.extra as x:
                     read-file{x}
                  else:
                     ""
            }
         }
      else:
         options


nav-template = """
template :: @minimal
lnnav :: dump!
.container % {body}
"""

defaults = {
   fonts = {
      body = "sans"
      nav = "$font-body"
      header = "$font-body"
      title = "$font-header"
      pre = "Monospace"
      code = "$font-pre"
   }
   "font-sizes" = {
      body = "20px"
      nav = "$font-size-body"
      dropdown = "$font-size-body"
      caret = "$font-size-nav * 0.8"
   }
   borders = {
      nav = "2px solid darken($background-nav, 20%)"
      "nav-dropdown" = "10px solid $background-nav-dropdown"
   }
   widths = {
      body = "700px"
      nav = "$width-body + 2 * $padding-body"
   }
   paddings = {
      body = "10px"
      nav = "$padding-body"
      td = "5px"
   }
   backgrounds = {
      body = "#fff"
      nav = "$background-body"
      "nav-select" = "tweak($background-nav, 20%)"
      "nav-dropdown" = "tweak($background-nav, 10%)"
      "nav-dropdown-select" = "tweak($background-nav-dropdown, 10%)"
      pre = "$background-body"
      code = "$background-body"
      table = "tweak($background-body, 10%)"
      "table-odd" = "tweak($background-table, 5%)"
      "table-header" = "tweak($background-table, 20%)"
   }
   colors = {
      body = "tweak($background-body, 85%)"
      nav = "bwtext($background-nav)"
      "nav-select" = "bwtext($background-nav-select)"
      "nav-dropdown" = "bwtext($background-nav-dropdown)"
      "nav-dropdown-select" = "bwtext($background-nav-dropdown-select)"
      link = "contrasting($background-body, #aaf, #00f)"
      "link-visited" = "contrasting($background-body, #f8f, #808)"
      pre = "bwtext($background-pre)"
      code = "bwtext($background-code)"
      table = "bwtext($background-table)"
      "table-odd" = "bwtext($background-table-odd)"
      "table-header" = "bwtext($background-table-header)"
      header = "$color-body"
      title = "$color-header"
   }
}

install{@, _options} =


   options = read-sheets{@, deepmerge{defaults, _options}}
   sheets = options._sheets
   var sass-sheet = sheets.main

   if options.font:
      options.fonts.body = options.font

   if options.use-nav:
      sass-sheet += sheets.nav

   variables = {
      "use-nav" => options.use-nav
   }

   google-fonts = options.google-fonts or {}

   {.background, .color, "font-size"
    .border, .padding, .margin, .width} each type ->
      items{options['{type}s'] or {=}} each {location, value} ->
         variables['{type}-{location}'] = value

   items{options.fonts} each {location, _name} ->
      name = match _name:
         R"^g:([^:]+)(:.*)?"! {_, family, settings} ->
            google-fonts.push{family + [settings or ""]}
            family
         else ->
            _name
      variables['font-{location}'] = name

   res = node-sass.render-sync{
      data = sheets.common + varpfx.join{"\n"} + sass-sheet + sheets.extra
      indented-syntax = true
   } where
      varpfx = items{variables} each {k, v} -> '${k}: {v}'

   sheet = postcss{{autoprefixer}}.process{res.css.to-string{}}.css

   quaint-google-fonts{@, fonts = google-fonts}
   quaint-nav{@}

   @register-resources with {
      "style/main.css" => {
         path = null
         contents = sheet
         type = .css
         method = .head
      }
      "js/nav.js" => {
         path = path.join{__dirname, "../js/nav.js"}
         contents = true
         type = .js
         method = .head
      }
      "js/nav.js/install" => {
         path = null
         contents = "$$quaintNavFunctionality()"
         type = .js
         method = .body
         inline = true
      }
   }

   @register-resolvers with {
      template = {
         "@nav" => nav-template
      }
   }

   @register-macros with {
      lnnav{engine, body} =
         if body.raw{}.trim{} !== "dump!":
            throw E.quaint-look-nice with
               'lnnav:: macro can only have "dump!" as its body'
         {
            .nav-container-desktop %
               quaint-nav.dump-nav{engine, "leftmost"}
               quaint-nav.dump-nav{engine, "main"}
               quaint-nav.dump-nav{engine, "rightmost"}
            .nav-container-mobile %
               nav.nav-mobile-menu.dropdown %
                  .hamburger %
                     span.icon-bar %
                     span.icon-bar %
                     span.icon-bar %
                  quaint-nav.dump-nav{engine, "mobile-menu", ulonly = true}
               quaint-nav.dump-nav{engine, "mobile"}
         }
   }



main{*match} =
   {@, options = {=}} when @is-quaint-engine ->
      install{@, options}
   {_options = {=}} ->
      options = read-sheets{null, _options}
      {@} -> main{@, options}

provide = main

