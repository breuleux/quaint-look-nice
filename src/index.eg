
require:
   quaint-google-fonts
   quaint-nav
   path
   fs
   node-sass
   deepmerge

common-sheet = fs.read-file-sync{path.join{__dirname, "../style/_common.sass"}, .utf8}
main-sheet = fs.read-file-sync{path.join{__dirname, "../style/nice.sass"}, .utf8}
nav-sheet = fs.read-file-sync{path.join{__dirname, "../style/nav.sass"}, .utf8}

defaults = {
   fonts = {
      body = "sans"
      nav = "$font-body"
      heading = "$font-body"
      pre = "Monospace"
      code = "$font-pre"
   }
   "font-sizes" = {
      body = "20px"
      nav = "$font-size-body"
      dropdown = "$font-size-body"
      caret = "$font-size-nav * 0.8"
   }
   borders = {
      nav = "2px solid darken($background-nav, 20%)"
   }
   widths = {
      body = "700px"
      nav = "$width-body + 2 * $padding-body"
   }
   paddings = {
      body = "10px"
      nav = "$padding-body"
      td = "5px"
   }
   backgrounds = {
      body = "#fff"
      nav = "$background-body"
      "nav-select" = "tweak($background-nav, 20%)"
      "nav-dropdown" = "tweak($background-nav, 10%)"
      "nav-dropdown-select" = "tweak($background-nav-dropdown, 10%)"
      pre = "$background-body"
      code = "$background-body"
      table = "tweak($background-body, 10%)"
      "table-odd" = "tweak($background-table, 5%)"
      "table-header" = "tweak($background-table, 20%)"
   }
   colors = {
      body = "tweak($background-body, 85%)"
      nav = "bwtext($background-nav)"
      "nav-select" = "bwtext($background-nav-select)"
      "nav-dropdown" = "bwtext($background-nav-dropdown)"
      "nav-dropdown-select" = "bwtext($background-nav-dropdown-select)"
      link = "contrasting($background-body, #aaf, #00f)"
      "link-visited" = "contrasting($background-body, #f8f, #808)"
      pre = "bwtext($background-pre)"
      code = "bwtext($background-code)"
      table = "bwtext($background-table)"
      "table-odd" = "bwtext($background-table-odd)"
      "table-header" = "bwtext($background-table-header)"
      header = "$color-body"
   }
}

install{@, _options} =

   var sass-sheet = main-sheet;

   options = deepmerge{defaults, _options}
   if options.font:
      options.fonts.body = options.font
   if options.use-nav:
      sass-sheet += nav-sheet

   variables = {
      "use-nav" => options.use-nav
   }

   google-fonts = options.google-fonts or {}

   {.background, .color, "font-size"
    .border, .padding, .margin, .width} each type ->
      items{options['{type}s'] or {=}} each {location, value} ->
         variables['{type}-{location}'] = value

   items{options.fonts} each {location, _name} ->
      name = match _name:
         R"^g:([^:]+)(:.*)?"! {_, family, settings} ->
            google-fonts.push{family + [settings or ""]}
            family
         else ->
            _name
      variables['font-{location}'] = name

   extra-sheet =
      if options.extra:
         @read-resource-text{.resource, options.extra}
      else:
         ""

   res = node-sass.render-sync{
      data = common-sheet + varpfx.join{"\n"} + sass-sheet + extra-sheet
      indented-syntax = true
   } where
      varpfx = items{variables} each {k, v} -> '${k}: {v}'
   sheet = res.css.to-string{}

   quaint-google-fonts{@, fonts = google-fonts}
   quaint-nav{@}

   @register-resources with {
      "style/nice.css" => {
         path = null
         contents = sheet
         type = .css
         method = .head
      }
   }

main{*match} =
   {@, options = {=}} when @is-quaint-engine ->
      install{@, options}
   {options = {=}} ->
      {@} -> main{@, options}

provide = main

